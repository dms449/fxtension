import java.util.regex.Matcher
import java.util.regex.Pattern

apply plugin: 'java-library'
apply plugin: 'idea'

// Java version required
sourceCompatibility = 1.8

/*** CONFIGURATION SECTION ***/
ext {
    // WeatherTools Version Number
    //   The version number is preferentially obtained from git
    //   tags, where available. If the information cannot be
    //   retrieved from Git, this version will be used instead.
    //
    //   Version numbers are semantic and are formatted
    //   MAJOR.MINOR.PATCH.
    SettingsDialogVersion = '0.3.0'

    // default locations for external java libs
    String osName = System.getProperty("os.name").toLowerCase()
    defaultDependencyDirs =  (osName.contains("windows")) ? defaultDependencyDirs = ["C:/JavaLibs"] :[System.getProperty("user.home") + "/JavaLibs"];

    // the repo location for external dependencies can be provided with the command line argument -Prepo=...
    // Otherwise it will default to the above locations
    localRepo = project.hasProperty("repo") ? project.property("repo") : defaultDependencyDirs
}


group = 'stevensd.settings'
archivesBaseName = 'settings-dialog'
version = git_describe()

// Define a local directory with java jars to use as a mavenLocal repo
repositories {
    flatDir {
        dirs project.property("localRepo")
    }
}

sourceSets {
    generated {
        java.srcDir "${buildDir}/generated/"
    }
}

dependencies{
    testImplementation name: 'hamcrest-all', version:'1.3'
    testImplementation name: 'junit', version:'4.12'
    api sourceSets.generated.output.classesDirs
}

/**
 * Task: Generate Java class with version information
 */
task generateSources {
    inputs.property "version", project.version
    outputs.dir "$buildDir/generated"

    doFirst {
        def versionFile = file("$buildDir/generated/stevensd/settings/SettingsDialogVersion.java")
        versionFile.parentFile.mkdirs()
        versionFile.text =
                """
    package stevensd.settings;

    public class SettingsDialogVersion {
      public static String getVersion() {
        return "$project.version";
      }
    }
    """
    }
}

// Always generate sources first
compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Specification-Title': archivesBaseName,
                'Implementation-Version': version,
                'Implementation-Vendor': 'U.S. Army AMRDEC',
                'Assembly-Date': new java.util.Date().toString()
    }
    from {
        configurations.compileClasspath.collect {it.isDirectory() ? it :zipTree(it)}
    }
}

/**
 * Intellij
 */
idea {
    module {
        generatedSourceDirs += new File(buildDir, "generated")
    }
}

/**
 * Returns the git description of the software build.
 * If git is not found, then the fallback version is used instead
 * @return
 */
def git_describe() {
    def git_description = "${SettingsDialogVersion}";
    try{
        git_description = "git -C ${projectDir} describe --long --always HEAD".execute().text.trim()
    } catch (java.lang.Exception e){
        /// do nothing
    }
//    git_description.substring(0, git_description.indexOf("-"))
    git_description
} // git_describe()
